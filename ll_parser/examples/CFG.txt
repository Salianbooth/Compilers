Program → PPList DeclList StmtList
PPList → PPDirective PPList
PPList → ε
DeclList → Decl DeclList
DeclList → ε
PPDirective → # include < ID . ID >
Decl → Type IDList
IDList → ID DeclTail
DeclTail → ( ParamList ) CompoundStmt
DeclTail → VarDeclTail
VarDeclTail → VarDeclPrime
VarDeclTail → , IDList
VarDeclPrime → = Expr ;
VarDeclPrime → ;
Type → int
Type → float
Type → void
ParamList → Param ParamListTail
ParamList → ε
ParamListTail → , Param ParamListTail
ParamListTail → ε
Param → Type ID
Stmt → Decl
Stmt → AssignStmt
Stmt → ExprStmt
Stmt → CompoundStmt
Stmt → IfStmt
Stmt → WhileStmt
Stmt → ForStmt
Stmt → ReturnStmt
Stmt → CallStmt
AssignStmt → ID = Expr ;
CallStmt → ID ( ArgList ) ;
ExprStmt → Expr ;
ExprStmt → ;
IfStmt → if ( Expr ) Stmt ElseStmt
ElseStmt → else Stmt
ElseStmt → ε
WhileStmt → while ( Expr ) Stmt
ForStmt → for ( Expr ; Expr ; Expr ) Stmt
ReturnStmt → return Expr ;
CompoundStmt → { DeclList StmtList }
StmtList → Stmt StmtList
StmtList → ε
Expr → ExprAssign
ExprAssign → ExprOr AssignTail
AssignTail → = ExprAssign
AssignTail → ε
ExprOr → ExprAnd ExprOrTail
ExprOrTail → || ExprAnd ExprOrTail
ExprOrTail → ε
ExprAnd → ExprRel ExprAndTail
ExprAndTail → && ExprRel ExprAndTail
ExprAndTail → ε
ExprRel → ExprAdd ExprRelTail
ExprRelTail → == ExprAdd
ExprRelTail → != ExprAdd
ExprRelTail → <= ExprAdd
ExprRelTail → >= ExprAdd
ExprRelTail → < ExprAdd
ExprRelTail → > ExprAdd
ExprRelTail → ε
ExprAdd → ExprMul ExprAddTail
ExprAddTail → + ExprMul ExprAddTail
ExprAddTail → - ExprMul ExprAddTail
ExprAddTail → ε
ExprMul → ExprUnary ExprMulTail
ExprMulTail → * ExprUnary ExprMulTail
ExprMulTail → / ExprUnary ExprMulTail
ExprMulTail → % ExprUnary ExprMulTail
ExprMulTail → ε
ExprUnary → ( ExprCastOrNormal
ExprUnary → + ExprUnary
ExprUnary → - ExprUnary
ExprUnary → ! ExprUnary
ExprUnary → ++ ExprUnary
ExprUnary → -- ExprUnary
ExprUnary → ExprPostfix
ExprCastOrNormal → Type ) ExprUnary
ExprCastOrNormal → Expr )
ExprPostfix → ExprPrimary ExprPostfixTail
ExprPostfixTail → ( ArgList ) ExprPostfixTail
ExprPostfixTail → [ Expr ] ExprPostfixTail
ExprPostfixTail → ++ ExprPostfixTail
ExprPostfixTail → -- ExprPostfixTail
ExprPostfixTail → ε
ExprPrimary → ID
ExprPrimary → INT_LITERAL
ExprPrimary → FLOAT_LITERAL
ExprPrimary → STRING_LITERAL
ExprPrimary → CHAR_LITERAL
ExprPrimary → read ( )
ExprPrimary → write ( Expr )
ArgList → Expr ArgListTail
ArgList → ε
ArgListTail → , Expr ArgListTail
ArgListTail → ε